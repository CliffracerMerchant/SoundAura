plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id "com.mikepenz.aboutlibraries.plugin"
}

android {
    compileSdk 33

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    defaultConfig {
        applicationId "com.cliffracertech.soundaura"
        minSdk 24
        targetSdk 33
        versionCode 8
        versionName "1.4.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation":"$projectDir/schemas".toString()]
            }
        }
        kapt.arguments { arg("room.schemaLocation", "$projectDir/schemas") }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            shrinkResources false
            debuggable true
        }
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_9
        targetCompatibility JavaVersion.VERSION_1_9
        coreLibraryDesugaringEnabled true
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.2.0'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    testOptions.unitTests.includeAndroidResources = true
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        freeCompilerArgs += [
            '-opt-in=kotlin.RequiresOptIn',
            '-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi',
            '-opt-in=androidx.compose.animation.ExperimentalAnimationApi',
            '-opt-in=androidx.compose.animation.graphics.ExperimentalAnimationGraphicsApi',
            '-opt-in=androidx.compose.ui.ExperimentalComposeUiApi',
            '-opt-in=androidx.compose.foundation.ExperimentalFoundationApi',
            '-opt-in=androidx.compose.material3.windowsizeclass.ExperimentalMaterial3WindowSizeClassApi',
        ]
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation "androidx.compose.ui:ui:1.3.3"
    implementation "androidx.compose.material:material:1.3.1"
    implementation "androidx.compose.ui:ui-tooling-preview:1.3.3"
    implementation 'androidx.compose.animation:animation:1.3.3'
    implementation 'androidx.compose.animation:animation-graphics:1.3.3'
    implementation 'androidx.appcompat:appcompat:1.6.0'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.activity:activity-compose:1.6.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-service:2.5.1'
    implementation 'androidx.room:room-runtime:2.5.0'
    implementation 'androidx.room:room-ktx:2.5.0'
    implementation 'androidx.compose.material:material-icons-extended:1.3.1'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1"
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.5.1'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation "com.google.accompanist:accompanist-systemuicontroller:0.17.0"
    implementation "com.google.accompanist:accompanist-insets:0.23.1"
    implementation "com.google.accompanist:accompanist-insets-ui:0.23.1"
    implementation "androidx.media:media:1.6.0"
    implementation "androidx.datastore:datastore-preferences:1.0.0"
    implementation "com.google.dagger:hilt-android:2.42"
    implementation "com.mikepenz:aboutlibraries-core:10.0.0"
    implementation "com.mikepenz:aboutlibraries-compose:10.0.0"
    implementation "androidx.compose.material3:material3-window-size-class:1.1.0-alpha04"
    implementation 'org.jetbrains.kotlinx:kotlinx-collections-immutable:0.3.5'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.2.0'

    kapt "androidx.room:room-compiler:2.5.0"
    kapt "com.google.dagger:hilt-compiler:2.42"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.test:core-ktx:1.4.0'
    testImplementation 'androidx.test.ext:junit-ktx:1.1.3'
    testImplementation "org.robolectric:robolectric:4.5.1"
    testImplementation 'androidx.test.ext:truth:1.4.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4'
    debugImplementation "androidx.compose.ui:ui-test-manifest:1.3.0"
    debugImplementation "androidx.compose.ui:ui-tooling:1.3.0"
    debugImplementation "androidx.compose.ui:ui-test-junit4:1.3.0"

    androidTestImplementation 'androidx.test.ext:truth:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:1.3.0"
    androidTestImplementation 'androidx.room:room-testing:2.4.3'
    androidTestImplementation 'androidx.test:rules:1.4.0'
}